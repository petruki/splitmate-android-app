plugins {
    id 'com.android.application'
}

String getProperties(String propertyName) {
    String value
    if (hasProperty(propertyName)) {
        value = this.getProperty(propertyName)
    } else {
        def apiKeyPropertiesFile = rootProject.file("secrets.properties")
        def apiKeyProperties = new Properties()
        apiKeyProperties.load(new FileInputStream(apiKeyPropertiesFile))
        value = apiKeyProperties[propertyName]
    }
    return value
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.trackerforce.splitmate"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "v1.0.6"
        manifestPlaceholders.screenOrientation = "portrait"

        buildConfigField("String", "API_URL", "\"${getProperties("api_url")}\"")
        buildConfigField("String", "API_KEY", "\"${getProperties("secret")}\"")
        buildConfigField("String", "GOOGLE_RECAPTCHA_KEY", "\"${getProperties("google_recaptcha_key")}\"")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.navigation:navigation-fragment:2.3.5'
    implementation 'androidx.navigation:navigation-ui:2.3.5'
    implementation 'androidx.preference:preference:1.1.1'
    implementation ('io.socket:socket.io-client:1.0.0') {
        exclude group: 'org.json', module: 'json'
    }

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    implementation 'com.google.android.gms:play-services-safetynet:17.0.1'
    implementation 'com.google.android.gms:play-services-ads-lite:20.2.0'

    implementation 'com.github.petruki:dblite-library:0.1.2-rc'
}